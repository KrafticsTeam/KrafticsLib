plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

ext {
    project_name = 'KrafticsLib'
    project_version = System.properties['version'] != null ? System.properties['version'].toString() : "1.0.0"
    project_description = 'Spigot library to make plugin coding fun and easier'
    project_url = 'https://kraftics.com/krafticslib'
    project_jdk = '1.8'

    artifact_group = 'com.kraftics'

    isRelease = !project_version.toString().endsWith('-SNAPSHOT')
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = artifact_group
    version = project_version
    description = project_description

    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    repositories {
        mavenCentral()
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url "https://libraries.minecraft.net" }
    }

    dependencies {
        compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    }

    jar {
        from {
            if (configurations.compile.canBeResolved) {
                configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
            }
        }
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }
}

subprojects {
    archivesBaseName = "krafticslib-$project.name"

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                groupId = artifact_group
                artifactId = archivesBaseName
                version = project_version

                pom {
                    name = project_name
                    description = project_description
                    url = project_url

                    organization {
                        name = 'Kraftics'
                        url = 'http://kraftics.com'
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://raw.githubusercontent.com/KrafticsTeam/KrafticsLib/master/LICENSE'
                            distribution = 'repo'
                        }
                    }

                    scm {
                        url = 'https://github.com/KrafticsTeam/KrafticsLib'
                        connection = 'scm:git:git://github.com/KrafticsTeam/KrafticsLib.git'
                        developerConnection = 'scm:git:ssh://git@github.com:KrafticsTeam/KrafticsLib.git'
                    }

                    developers {
                        developer {
                            name = 'Kraftics Team'
                        }
                    }
                }

            }
        }

        repositories {
            maven {
                if (isRelease) {
                    url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                } else {
                    url 'https://oss.sonatype.org/content/repositories/snapshots/'
                }

                def sonatypeUsername = findProperty('sonatypeUsername')
                def sonatypePassword = findProperty('sonatypePassword')
                if (sonatypeUsername != null && sonatypePassword != null) {
                    credentials {
                        username = sonatypeUsername
                        password = sonatypePassword
                    }
                }
            }
        }
    }

    if (isRelease) {
        signing {
            def signingKey = findProperty('signingKey')
            def signingPassword = findProperty('signingPassword')
            if (signingKey != null && signingPassword != null) {
                useInMemoryPgpKeys(signingKey as String, signingPassword as String)
            }
            sign publishing.publications.mavenJava
        }
    }
}

gradle.buildFinished {
    project.buildDir.deleteDir()
}